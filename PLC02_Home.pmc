; Homing PLC for Axis $(AXIS) (PLC $(PLC))
; 
; This program will home an axis to either the positive or negative limit. The
; homing program performs the following steps:
; 1. Move to the limit switch being used as a reference.
; 2. Perform a homing move away from the limit switch until it deactivates.
; 3. Return to the starting position. 
; 
; Note that the direction set by the home velocity (Ixx23) is opposite to the
; limit switch actually used for homing. This is because the homing movement
; (Step 2 above) is away from the limit, with the controller looking for the
; switch to deactivate to indicate the home position. This is summarised below:
; Home at negative limit -> Positive home velocity
; Home at positive limit -> Negative home velocity
; 
; Wayne Lewis 20140108
; 
; Macros:
; - PLC: PLC number
; - AXIS: motion axis
; - TIMER: PMAC timer to use
; - SERVO: first three digits of axis gate array (I7mn)

; P variables
; P$(PLC)00 - homing state (homing, not homing)
; P$(PLC)01 - homing status (limit search, home search, post-home move)
; P$(PLC)02 - post-home move (0 = return to previous, 1 = stay at zero)

; Initial configuration of state variables.
; Homing State P Variable
P200 = 0

; Homing Status P Variable
P201 = 0

DEL GAT
OPEN PLC2 CLEAR
P201 = 1

;---- Configuring State ----
P200=1
;Save high soft limits to P variables Pxx04..x19
P204=I813
;Save the low soft limits to P variables Pxx20..x35
P220=I814
;Save the home capture flags to P variables Pxx36..x51
P236=I7142
;Store value to use in moving off a flag in P variables Pxx52..x67
P252=10
;Save the limit flags to P variables Pxx68..x83
P268=I824
;Save the current position to P variables Pxx84..x99
P284=M862
;Clear the soft limits
I813=0
I814=0

if (P201=1)

	;---- FastSearch State ----
  ; Move to the limit switch
	if (P201=1)
		P200=8
		; Execute the move commands
		M872=100000000*(-I823/ABS(I823))
		cmd "#8J:*"
		; Wait for the move to complete
		I5111 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5111 > 0)
		endw
		I5111 = 600000 * 8388608/I10 ; Now start checking the conditions
		while (M840=0) ; At least one motor should not be In Position
		and (M842=0) ; No following errors should be set for any motor
		and (I5111 > 0) ; Check for timeout
		and (P201 = 1) ; Check that we didn't abort
		endw

		; Check why we left the while loop
		if (M842=1) ; If a motor hit a following error
			P201 = 4
		endif
		if (I5111<0 or I5111=0) ; If we timed out
			P201 = 3
		endif
	endif

	;---- Store the difference between current pos and start pos ----
	if (P201=1)
		P286=(P286-M862)/(I808*32)-(I826/16)
	endif

	;---- Homing State ----
	if (P201=1)
		P200=4
    ; Set the capture flag correctly            
    ; If homing direction is negative, we are homing using the positive
    ; limit switch and vice versa.              
    if (I823<0)
      I7143=1
    else
      I7143=2
    endif             
		; Execute the move commands
		I7142=P252
		cmd "#8hm"
		; Wait for the move to complete
		I5111 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5111 > 0)
		endw
		I5111 = 600000 * 8388608/I10 ; Now start checking the conditions
    P205 = 0
		while (M840=0) ; At least one motor should not be In Position
		and (M842=0) ; No following errors should be set for any motor
		and (P205=0) ; Should not stop on position limit for selected motors
		and (I5111 > 0) ; Check for timeout
		and (P201 = 1) ; Check that we didn't abort
    if (I823 < 0 and M832 = 1)
      P205 = 1
    endif
    if (I823 > 0 and M831 = 1)
      P205 = 1
    endif
		endw
		; Check why we left the while loop
		if (M842=1) ; If a motor hit a following error
			P201 = 4
		endif
		if (M830=1) ; If a motor hit a limit
			P201 = 5
		endif
		if (I5111<0 or I5111=0) ; If we timed out
			P201 = 3
		endif
	endif

	;---- Check if all motors have homed ----
	if (P201=1)
	and (M845=0)
		P201=6
	endif

	;---- PostHomeMove State ----
	if (P201=1)
    ; Check the type of post-home move
    if (P202 = 0)
      P200=5
      ; Execute the move commands
      M872=P284/(I808*32)
      cmd "#8J=*"
      ; Wait for the move to complete
      I5111 = 20 * 8388608/I10 ; Small delay to start moving
      while (I5111 > 0)
      endw
      I5111 = 600000 * 8388608/I10 ; Now start checking the conditions
      while (M840=0) ; At least one motor should not be In Position
      and (M842=0) ; No following errors should be set for any motor
      and (M830=0) ; Should not stop on position limit for selected motors
      and (I5111 > 0) ; Check for timeout
      and (P201 = 1) ; Check that we didn't abort
      endw
      ; Check why we left the while loop
      if (M842=1) ; If a motor hit a following error
        P201 = 4
      endif
      if (M830=1) ; If a motor hit a limit
        P201 = 5
      endif
      if (I5111<0 or I5111=0) ; If we timed out
        P201 = 3
      endif
    endif
	endif

endif

;---- Done ----
if (P201 = 1)
	;If we've got this far without failing, set status and state done
	P201=0
	P200=7
endif

;---- Tidy Up ----
;Stop all motors if they don't have a following error
if (M842=0)
	cmd "#8J/"
endif
;Restore the high soft limits from P variables Pxx04..x19
I813=P204
;Restore the low soft limits from P variables Pxx20..x35
I814=P220
;Restore the home capture flags from P variables Pxx36..x51
I7142=P236
;Restore the limit flags to P variables Pxx68..x83
I824=P268

DISABLE PLC2
CLOSE


